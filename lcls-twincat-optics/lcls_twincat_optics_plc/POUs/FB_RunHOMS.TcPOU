<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_RunHOMS" Id="{ee7498ff-903e-47a0-a649-7587adeefcee}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RunHOMS
VAR_INPUT
	// Encoder Reference Values
	nYupEncRef : ULINT;
	nYdwnEncRef : ULINT;
	nXupEncRef : ULINT;
	nXdwnEncRef : ULINT;

	// Gantry Tolerances
	nGantryTolY : LINT := GVL_Constants.nGANTRY_TOLERANCE_NM_DEFAULT; // Encoder counts = nm
	nGantryTolX : LINT := GVL_Constants.nGANTRY_TOLERANCE_NM_DEFAULT; // Encoder counts = nm
	
	// PMPS States
	nTransitionAssertionID: UDINT;
    nUnknownAssertionID: UDINT;
	sCoating1Type : STRING := ''; //Type of Material on Coating 1
	nCoating1Position: UDINT; // Encoder Count Position of Coating 1
	fCoating1Delta: LREAL;//Position Boundary delta in scaled position units
	stCoating1BeamParams: ST_BeamParams:= PMPS_GVL.cst0RateBeam;; //Safe Beam Parameters for the first coating
	sCoating2Type : STRING := '';// Type of Material on Coating 2
	nCoating2Position: UDINT; // Encoder Count Position of Coating 2
	fCoating2Delta: LREAL;//Position Boundary delta in scaled position units
	stCoating2BeamParams: ST_BeamParams:= PMPS_GVL.cst0RateBeam; //Safe Beam Parameters for the second coating
END_VAR
VAR_OUTPUT
	// Gantry coupling status
	bGantryAlreadyCoupledY : BOOL;
	bGantryAlreadyCoupledX : BOOL;
	
	// Current gantry difference
	nCurrGantryY : LINT;
	nCurrGantryX : LINT;
END_VAR
VAR_IN_OUT
	// Motor Structs
	stYup : DUT_MotionStage;
	stYdwn : DUT_MotionStage;
	stXup : DUT_MotionStage;
	stXdwn : DUT_MotionStage;
	stPitch : DUT_MotionStage;

	// Manual coupling Gantried Axes
	bExecuteCoupleY : BOOL;
	bExecuteCoupleX : BOOL;
	bExecuteDecoupleY : BOOL;
	bExecuteDecoupleX : BOOL;
	
	// States PMPS
	fbArbiter: FB_Arbiter;
    fbFFHWO: FB_HardwareFFOutput;
END_VAR
VAR
	// States Init
	 bInit: BOOL :=TRUE;
	 {attribute 'pytmc' := '
        pv: COATING:STATE
        io: i
    '}
    fbStates: FB_Coatings_States;
	 
	// STO Button
	bSTOEnable1 AT %I* : BOOL;
	bSTOEnable2 AT %I* : BOOL;

	// Encoders
	stYupEnc AT %I* : ST_RenishawAbsEnc;
	stYdwnEnc AT %I* : ST_RenishawAbsEnc;
	
	stXupEnc AT %I* : ST_RenishawAbsEnc;
	stXdwnEnc AT %I* : ST_RenishawAbsEnc;

	// Autocoupling Gantried Axes
	fbAutoCoupleY : FB_GantryAutoCoupling;
	fbAutoCoupleX : FB_GantryAutoCoupling;
	fAccel: LREAL;
END_VAR

VAR CONSTANT
    fVelocity: LREAL := 0.01;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bInit THEN
	bInit := FALSE;
	
	// First coating Material
	fbStates.stCoating1.nRequestAssertionID := nTransitionAssertionID+1;
	fbStates.stCoating1.sName := sCoating1Type;
	fbStates.stCoating1.nEncoderCount := nCoating1Position;
	fbStates.stCoating1.stBeamParams := stCoating1BeamParams;
	fbStates.stCoating1.fVelocity := fVelocity;
	fbStates.stCoating1.fDelta := fCoating1Delta;
	fbStates.stCoating1.fAccel := fAccel;
	fbStates.stCoating1.fDecel := fAccel;

	//Second Coating Material
	fbStates.stCoating2.nRequestAssertionID := nTransitionAssertionID+2;
	fbStates.stCoating2.sName := sCoating2Type;
	fbStates.stCoating2.nEncoderCount := nCoating2Position;
	fbStates.stCoating2.stBeamParams := stCoating2BeamParams;
	fbStates.stCoating2.fVelocity := fVelocity;
	fbStates.stCoating2.fDelta := fCoating2Delta;
	fbStates.stCoating2.fAccel := fAccel;
	fbStates.stCoating2.fDecel := fAccel;
END_IF



// Encoder Reference Values
stYupEnc.Ref := nYupEncRef;
stYdwnEnc.Ref := nYdwnEncRef;
stXupEnc.Ref := nXupEncRef;
stXdwnEnc.Ref := nXdwnEncRef;

// Gantry Differences to monitor
nCurrGantryY := ((ULINT_TO_LINT(stYupEnc.Count) - ULINT_TO_LINT(stYupEnc.Ref)) - (ULINT_TO_LINT(stYdwnEnc.Count) - ULINT_TO_LINT(stYdwnEnc.Ref)));
nCurrGantryX := ((ULINT_TO_LINT(stXupEnc.Count) - ULINT_TO_LINT(stXupEnc.Ref)) - (ULINT_TO_LINT(stXdwnEnc.Count) - ULINT_TO_LINT(stXdwnEnc.Ref)));

// Release the hounds!
stYup.bHardwareEnable := bSTOEnable1 AND bSTOEnable2;
stYdwn.bHardwareEnable := bSTOEnable1 AND bSTOEnable2;
stXup.bHardwareEnable := bSTOEnable1 AND bSTOEnable2;
stXdwn.bHardwareEnable := bSTOEnable1 AND bSTOEnable2;
stPitch.bHardwareEnable := bSTOEnable1 AND bSTOEnable2;

// Start Autocoupling
fbAutoCoupleY(nGantryTol:=nGantryTolY,
	          Master:=stYup,
			  MasterEnc:= stYupEnc,
			  Slave:=stYdwn,
			  SlaveEnc:=stYdwnEnc,
			  bExecuteCouple:=bExecuteCoupleY,
			  bExecuteDecouple:=bExecuteDecoupleY,
			  bGantryAlreadyCoupled=>bGantryAlreadyCoupledY);

fbAutoCoupleX(nGantryTol:=nGantryTolX,
	          Master:=stXup,
			  MasterEnc:= stXupEnc,
			  Slave:=stXdwn,
			  SlaveEnc:=stXdwnEnc,
			  bExecuteCouple:=bExecuteCoupleX,
			  bExecuteDecouple:=bExecuteDecoupleX,
			  bGantryAlreadyCoupled=>bGantryAlreadyCoupledX);
			  
//Coatings States With PMPS
fbStates(
    fbArbiter:=fbArbiter,
    fbFFHWO:=fbFFHWO,
    nTransitionAssertionID:=nTransitionAssertionID,
    nUnknownAssertionID:=nUnknownAssertionID,
    stMotionStage:=stYup, // Y Gantry Master Axis
    bEnable:=TRUE);]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>